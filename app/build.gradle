apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    testOptions {
        unitTests.returnDefaultValues = true
    }
    defaultConfig {
        applicationId "com.terminatingcode.android.migrainetree"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 3
        versionName "1.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
        signingConfig signingConfigs.config
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':volley')
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.json:json:20160212'
    testCompile 'org.robolectric:robolectric:3.1.1'
    testCompile 'org.robolectric:shadows-support-v4:3.1.1'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        // Espresso does not work with v23+
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
        // Espresso does not work with v23+
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:runner:0.5') {
        // Espresso does not work with v23+
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.0-beta1'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.amazonaws:aws-android-sdk-core:2.2.18'
    compile 'com.amazonaws:aws-android-sdk-cognito:2.2.18'
    compile 'com.amazonaws:aws-android-sdk-ddb:2.2.18'
    compile 'com.amazonaws:aws-android-sdk-ddb-mapper:2.2.18'
    compile 'com.amazonaws:aws-android-sdk-mobileanalytics:2.2.18'
    compile 'com.amazonaws:aws-android-sdk-lambda:2.2.18'
    compile 'com.amazonaws:aws-android-sdk-sns:2.2.18'
    compile 'com.amazonaws:aws-android-sdk-machinelearning:2.2.18'
    compile 'com.google.android.gms:play-services-gcm:7.8.0'
    compile 'com.google.android.gms:play-services-plus:7.8.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
}
